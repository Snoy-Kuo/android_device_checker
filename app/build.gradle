plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'  // Google Services plugin
}

//ref= https://stackoverflow.com/questions/25854849/use-a-local-properties-field-when-declaring-a-buildconfigfield
def getLocalProps(String propName) {
    def propsFile = rootProject.file('local.properties')
    String value = ""
    if (propsFile.exists()) {
        def props = new Properties()
        try {
            props.load(new FileInputStream(propsFile))
            value = props[propName]
        } catch (Exception ignored) {
        }

    }
    return value
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.snoy.devicechecker"
        minSdk 22
        targetSdk 31
        versionCode 1
        versionName "1.0"

        //add gcp_api_key="YOUR_GCP_API_KEY" in local.properties
        buildConfigField "String", "API_KEY", getLocalProps("gcp_api_key")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //AAID
    // Used for the calls to addCallback() in the snippets on this page.
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    //coroutine for get AAID
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
    // Lifecycles only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    //

    //external IP
    implementation 'com.github.chintan369:Ipify-Android:1.0.1'
    //

    //FID
    //Create a Firebase project. Then add google-services.json in app module.
    implementation platform('com.google.firebase:firebase-bom:29.1.0')
    implementation 'com.google.firebase:firebase-installations:17.0.0'

    //SafetyNET
    implementation 'com.google.android.gms:play-services-safetynet:18.0.1'
    implementation 'com.google.code.gson:gson:2.9.0'
}